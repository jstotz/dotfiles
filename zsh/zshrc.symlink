#!/bin/zsh
if [ -n "${ZSH_DEBUGRC+1}" ]; then
    zmodload zsh/zprof
fi

zstyle ':plugin:ez-compinit' 'use-cache' 'yes'

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/.dotfiles"

# your project folder that we can `c [tab]` to
export PROJECTS="$HOME/Projects"

# your default editor
export EDITOR='nvim'
export VEDITOR='nvim'

# default C compiler
export CC="clang"

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load antidote plugins
zsh_plugins="$HOME/Library/Caches/antidote"
fpath=($HOMEBREW_PREFIX/opt/antidote/share/antidote/functions $fpath)
autoload -Uz antidote
source ~/.zsh_plugins.zsh

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files updated_at

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# shellcheck disable=SC1090
[ -f ~/.localrc ] && . ~/.localrc

# load atuin (shell history)
function zvm_after_init() {
  eval "$(atuin init zsh --disable-up-arrow)"
  bindkey -M vicmd 'k' atuin-history-up
  bindkey -M vicmd 'j' atuin-history-down
  bindkey "${key[Up]}" atuin-history-up
  bindkey "${key[Down]}" atuin-history-down
}

if [ -n "${ZSH_DEBUGRC+1}" ]; then
    zprof
fi
