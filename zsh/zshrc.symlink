#!/bin/zsh


mkdir -p $ZSH_CACHE_DIR

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load the prompt files
for file in ${(M)config_files:#*/prompt.zsh}; do
  source "$file"
done

# load antibody plugins
source ~/.bundles.txt

# load everything but the path, completion, and prompt files
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/prompt.zsh}; do
  source "$file"
done

autoload -Uz compinit
compinit -D

# load every completion before autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j' | sed 's/^0*//') != $updated_at ]; then
  compinit
else
  compinit -C
fi

unset config_files updated_at

# shellcheck disable=SC1090
[ -f ~/Private/localrc ] && . ~/Private/localrc

if [ $TILIX_ID ] || [ $VTE_VERSION ]; then
  source /etc/profile.d/vte.sh
fi
